class SQLParser::Parser

rule
  statement
    : select_statement

  subquery
    : left_paren select_statement right_paren { result = SQLParser::Statement::Subquery.new(val[1]) }

  select_statement
    : SELECT select_list from_clause where_clause group_by_clause having_clause order_by_clause limit_clause { result = SQLParser::Statement::Select.new(val[1], val[2], val[3], val[4], val[5], val[6], val[7]) }
    | SELECT select_list { result = SQLParser::Statement::Select.new(val[1]) }

  select_list
    : DISTINCT select_sublist { result = SQLParser::Statement::SelectList.new(val[1], true) }
    | select_sublist { result = SQLParser::Statement::SelectList.new(val[0]) }

  select_sublist
    : derived_column comma select_sublist { result = Array(val[0]) + Array(val[2]) }
    | derived_column

  derived_column
    : value_expression AS column_name { result = SQLParser::Statement::As.new(val[0], val[2]) }
    | value_expression column_name { result = SQLParser::Statement::As.new(val[0], val[1]) }
    | value_expression

  from_clause
    : FROM table_reference { result = SQLParser::Statement::FromClause.new(val[1]) }

  table_reference
    : table_name AS column_name { result = SQLParser::Statement::As.new(val[0], val[2]) }
    | table_name column_name { result = SQLParser::Statement::As.new(val[0], val[1]) }
    | table_name
    | joined_table

  table_subquery
    : subquery

  joined_table
    : cross_join
    | qualified_join

  cross_join
    : table_reference comma table_name { result = SQLParser::Statement::CrossJoin.new(val[0], val[2]) }
    | table_reference CROSS JOIN table_name { result = SQLParser::Statement::CrossJoin.new(val[0], val[3]) }

  qualified_join
    : table_reference join_type JOIN table_reference join_specification { result = val[1].new(val[0], val[3], val[4]) }

  join_type
    : INNER { result = SQLParser::Statement::InnerJoin }
    | LEFT OUTER { result = SQLParser::Statement::LeftOuterJoin }
    | LEFT { result = SQLParser::Statement::LeftJoin }
    | RIGHT OUTER { result = SQLParser::Statement::RightOuterJoin }
    | RIGHT { result = SQLParser::Statement::RightJoin }
    | FULL { result = SQLParser::Statement::FullJoin }
    | FULL OUTER { result = SQLParser::Statement::FullOuterJoin }

  join_specification
    : join_condition
    | named_columns_join

  join_condition
    : ON search_condition { result = SQLParser::Statement::On.new(val[1]) }

  named_columns_join
    : USING left_paren join_column_list right_paren { result = SQLParser::Statement::Using.new(val[2]) }

  join_column_list
    : column_name_list

  where_clause
    : # no action
    | WHERE search_condition { result = SQLParser::Statement::WhereClause.new(val[1]) }

  group_by_clause
    : # no action
    | GROUP BY grouping_column_reference_list { result = SQLParser::Statement::GroupByClause.new(val[2]) }

  grouping_column_reference_list
    : grouping_column_reference_list comma grouping_column_reference { result = Array(val[0]) + Array(val[2]) }
    | grouping_column_reference

  grouping_column_reference
    : column_reference

  having_clause
    : # no action
    | HAVING search_condition { result = SQLParser::Statement::HavingClause.new(val[1]) }

  order_by_clause
    : # no action
    | ORDER BY sort_specification_list { result = SQLParser::Statement::OrderBy.new(val[2]) }

  sort_specification_list
    : sort_specification_list comma sort_specification { result = Array(val[0]) + Array(val[2]) }
    | sort_specification

  sort_specification
    : sort_key ordering_specification { result = val[1].new(val[0]) }

  sort_key
    : column_name
    | unsigned_integer { result = SQLParser::Statement::Integer.new(val[0]) }

  ordering_specification
    : { result = SQLParser::Statement::Ascending } # default
    | ASC { result = SQLParser::Statement::Ascending }
    | DESC { result = SQLParser::Statement::Descending }

  limit_clause
    : # no action
    | LIMIT unsigned_integer OFFSET unsigned_integer { result = SQLParser::Statement::LimitClause.new(val[1], val[3]) }
    | LIMIT unsigned_integer comma unsigned_integer { result = SQLParser::Statement::LimitClause.new(val[3], val[1]) }
    | LIMIT unsigned_integer { result = SQLParser::Statement::LimitClause.new(val[1]) }

  row_subquery
    : subquery

  between_predicate
    : row_value_constructor NOT BETWEEN row_value_constructor AND row_value_constructor { result = SQLParser::Statement::Not.new(SQLParser::Statement::Between.new(val[0], val[3], val[5])) }
    | row_value_constructor BETWEEN row_value_constructor AND row_value_constructor { result = SQLParser::Statement::Between.new(val[0], val[2], val[4]) }

  in_predicate
    : row_value_constructor NOT IN in_predicate_value { result = SQLParser::Statement::Not.new(SQLParser::Statement::In.new(val[0], val[3])) }
    | row_value_constructor IN in_predicate_value { result = SQLParser::Statement::In.new(val[0], val[2]) }

  in_predicate_value
    : table_subquery
    | left_paren in_value_list right_paren { result = SQLParser::Statement::InValueList.new(val[1]) }

  in_value_list
    : value_expression comma in_value_list { result = Array(val[0]) + Array(val[2]) }
    | value_expression

  like_predicate
    # FIXME: the SQL-92 grammar indicates these should be
    # character_value_expression nodes, but changing them causes reduce/reduce
    # conflicts.
    : row_value_constructor NOT LIKE row_value_constructor { result = SQLParser::Statement::Not.new(SQLParser::Statement::Like.new(val[0], val[3])) }
    | row_value_constructor LIKE row_value_constructor { result = SQLParser::Statement::Like.new(val[0], val[2]) }

  null_predicate
    : row_value_constructor IS NOT NULL { result = SQLParser::Statement::Not.new(SQLParser::Statement::Is.new(val[0], SQLParser::Statement::Null.new)) }
    | row_value_constructor IS NULL { result = SQLParser::Statement::Is.new(val[0], SQLParser::Statement::Null.new) }

  exists_predicate
    : EXISTS table_subquery { result = SQLParser::Statement::Exists.new(val[1]) }

  table_name
    : identifier { result = SQLParser::Statement::Table.new(val[0]) }

  column_name_list
    : column_name_list comma column_name { result = Array(val[0]) + Array(val[2]) }
    | column_name

  search_condition
    : boolean_term
    | search_condition OR boolean_term { result = SQLParser::Statement::Or.new(val[0], val[2]) }

  boolean_term
    : boolean_factor
    | boolean_term AND boolean_factor { result = SQLParser::Statement::And.new(val[0], val[2]) }

  boolean_factor:
    : NOT boolean_test { result = SQLParser::Statement::Not.new(val[1]) }
    | boolean_test

  boolean_test
    : boolean_primary

  boolean_primary
    : predicate
    | left_paren search_condition right_paren { result = val[1] }

  predicate
    : comparison_predicate
    | between_predicate
    | in_predicate
    | like_predicate
    | null_predicate
    | exists_predicate

  comparison_predicate
    : row_value_constructor equals_operator row_value_constructor { result = SQLParser::Statement::Equals.new(val[0], val[2]) }
    | row_value_constructor not_equals_operator row_value_constructor { result = SQLParser::Statement::Not.new(SQLParser::Statement::Equals.new(val[0], val[2])) }
    | row_value_constructor less_than_operator row_value_constructor { result = SQLParser::Statement::Less.new(val[0], val[2]) }
    | row_value_constructor greater_than_operator row_value_constructor { result = SQLParser::Statement::Greater.new(val[0], val[2]) }
    | row_value_constructor less_than_or_equals_operator row_value_constructor { result = SQLParser::Statement::LessOrEquals.new(val[0], val[2]) }
    | row_value_constructor greater_than_or_equals_operator row_value_constructor { result = SQLParser::Statement::GreaterOrEquals.new(val[0], val[2]) }

  row_value_constructor
    : row_value_constructor_element
    | row_subquery

  row_value_constructor_element
    : value_expression

  value_expression
    : asterisk { result = SQLParser::Statement::All.new }
    | numeric_value_expression
    | general_literal

  numeric_value_expression
    : term plus_sign numeric_value_expression { result = SQLParser::Statement::Add.new(val[0], val[2]) }
    | term minus_sign numeric_value_expression { result = SQLParser::Statement::Subtract.new(val[0], val[2]) }
    | term

  term
    : factor asterisk term { result = SQLParser::Statement::Multiply.new(val[0], val[2]) }
    | factor solidus term { result = SQLParser::Statement::Divide.new(val[0], val[2]) }
    | factor

  factor
    : sign value_expression_primary { result = val[0].new(val[1]) }
    | value_expression_primary

  value_expression_primary
    : unsigned_value_specification
    | column_reference
    | function_specification
    | left_paren value_expression right_paren { result = val[1] }

  unsigned_value_specification
    : unsigned_literal

  unsigned_literal
    : unsigned_numeric_literal
    | general_literal

  column_reference
    : qualifier period column_name { result = SQLParser::Statement::QualifiedColumn.new(val[0], val[2]) }
    | column_name

  qualifier
    : table_name

  function_specification
    : function_name left_paren right_paren { result = SQLParser::Statement::Function.new(val[0]) }
    | function_name left_paren function_arguments right_paren { result = SQLParser::Statement::Function.new(val[0], val[2]) }

  function_name
    : identifier

  function_arguments
    : function_arguments comma value_expression { result = Array(val[0]) + Array(val[2]) }
    | value_expression

  unsigned_numeric_literal
    : unsigned_integer period unsigned_integer { result = SQLParser::Statement::Float.new("#{val[0]}.#{val[2]}".to_f) }
    | unsigned_integer period { result = SQLParser::Statement::Float.new(val[0]) }
    | period unsigned_integer { result = SQLParser::Statement::Float.new("0.#{val[1]}".to_f) }
    | unsigned_integer { result = SQLParser::Statement::Integer.new(val[0]) }

  signed_integer
    : sign unsigned_integer { result = val[0].new(SQLParser::Statement::Integer.new(val[1])) }
    | unsigned_integer { result = SQLParser::Statement::Integer.new(val[0]) }

  sign
    : plus_sign { result = SQLParser::Statement::UnaryPlus }
    | minus_sign { result = SQLParser::Statement::UnaryMinus }

  column_name
    : identifier { result = SQLParser::Statement::Column.new(val[0]) }

  general_literal
    : quote character_string_literal quote { result = SQLParser::Statement::String.new(val[1]) }
    | quote quote { result = SQLParser::Statement::String.new('') }
    | datetime_literal
    | boolean_literal

  datetime_literal
    : date_literal

  date_literal
    : DATE date_string { result = SQLParser::Statement::Date.new(val[1]) }

  boolean_literal
    : TRUE { result = SQLParser::Statement::True.new }
    | FALSE { result = SQLParser::Statement::False.new }

---- header ----
require File.dirname(__FILE__) + '/parser.rex.rb'

---- inner ----

def self.parse(sql)
  new.scan_str(sql)
rescue Racc::ParseError => err
  raise err.to_s.strip
end
